# entity:sculk_spider/summon
# called by: <dev>

$summon cave_spider ~ ~ ~ {$(nbt),CustomName:'{"text":"Sculk Spider"}',DeathLootTable:"magical_addons:entities/sculk_spider",Health:64.0,attributes:[{id:jump_strength,base:0.7d},{id:movement_speed,base:0.35d},{id:fall_damage_multiplier,base:0.0},{id:max_health,base:64.0},{id:scale,base:1.5d}],active_effects:[{id:"invisibility",amplifier:-1,duration:-1,show_particles:0b},{id:"fire_resistance",amplifier:-1,duration:-1,show_particles:0b}],Tags:["maddons.sculk_spider","maddons.vehicle"],Passengers:[{id:"item_display",teleport_duration:3,shadow_strength:1.5,shadow_radius:0.75,item:{id:"potion",components:{item_model:"magical_addons:entity/sculk_spider",custom_model_data:{strings:["body_idle_normal"],colors:[I;0]}}},view_range:300f,Tags:["maddons.entity","maddons.sculk_spider","maddons.body","maddons.part"],Passengers:[{id:"cave_spider",active_effects:[{id:"minecraft:invisibility",duration:-1,amplifier:-1,show_particles:0b},{id:"fire_resistance",amplifier:-1,duration:-1,show_particles:0b}],Silent:1,DeathLootTable:"",Tags:["maddons.sculk_spider"],CustomName:'{"text":"Sculk Spider"}',Invulnerable:1,PersistenceRequired:1,attributes:[{id:fall_damage_multiplier,base:0.0d},{id:follow_range,base:90.0d},{id:scale,base:0.0d}]}]},{id:"item_display",item:{id:"potion",components:{item_model:"magical_addons:entity/sculk_spider",custom_model_data:{strings:["head_idle_normal"],colors:[I;0]}}},view_range:300f,teleport_duration:3,Tags:["maddons.sculk_spider","maddons.head","maddons.part"]}]}
execute as @n[type=cave_spider,tag=maddons.vehicle,tag=maddons.sculk_spider] on passengers run data merge entity @s[tag=maddons.part] {transformation:{left_rotation:[0f,1f,0f,0f],translation:[0f,0.5f,0f]}}
execute as @n[type=cave_spider,tag=maddons.vehicle,tag=maddons.sculk_spider] on passengers as @s[tag=maddons.entity] run function magical_addons:dev/register_entity {id:"sculk_spider",cmd:"function magical_addons:entity/sculk_spider/loop"}
execute as @n[type=cave_spider,tag=maddons.vehicle,tag=maddons.sculk_spider] on passengers as @s[tag=maddons.entity] run function magical_addons:entity/sculk_spider/check